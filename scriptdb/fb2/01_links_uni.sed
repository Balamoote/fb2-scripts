#=========================================
# На основе «Генеральная уборка» v.2.2
# Engine by ©Sclex 01.05.2007 – 07.05.2008
#=========================================
# Последняя версия тут https://github.com/Balamoote/fb2-scripts

## Сноски - обрабатывает только сноски стандартного вида
# Приводим вид ссылок к "общему виду"
2 s~(<FictionBook xmlns="http://www.gribuser.ru/xml/fictionbook/2.0" xmlns:)[0-9a-z-]+(="http://www.w3.org/1999/xlink">)~\1l\2~gI
s~(<a )[^:]+(:href="#[^"]+" type="note"</a>)~\1l\2~gI
s~(<a type="note" )[^:]+(:href="#[^"]+"</a>)~\1l\2~gI
s~(<a)( type="note")( l:href="#[^"]+")(>[^<]+</a>)~\1\3\2\4~gI

# Приведение сноски к стандартному виду. Неприемлемо, если для каждого раздела существует свой формат префикса сноски
s~(<a l:href="#)[[:alnum:]]*[^0-9]([0-9]+)(" type="note">[^<]+</a>)~\1n_\2\3~gI
s~(<section id=")[[:alnum:]]*[^0-9]([0-9]+)(">)~\1n_\2\3~gI

# квадратное окавычивание порядкового номера в ссылке сноски - если n_ , то предполагаем примечание, а не комментарий
s~(<a l:href="#n_[0-9]+" type="note">)[[{]?([0-9]+)[]}]?(</a>)~\1[\2]\3~gI

# снятие курсива со сноски 
s~(<a l:href="#n_[0-9]+" type="note">[^<]+</a>)($fib){1,2}~\2\1~gI

# снятие курсива со сноски
s~($sib){1,2}([ .]{0,2}<a l:href="#n_[0-9]+" type="note">[^<]+</a>)~\2\1~gI

# удаление пробела после [сноски] , если за пробелом следует препинание
s~[ \xc2\xa0]?(<a l:href="#n_[0-9]+" type="note">[^<]+</a>)[ \xc2\xa0]?([,.;!?:…])?([ \xc2\xa0])?($fib){0,2}~\4\2\1\3~gI

# удаление пробела перед [сноской]
s~[ \xc2\xa0]?($aib){0,2}[ \xc2\xa0]?(<a l:href="#n_[0-9]+" type="note">[^<]+</a>)([^ .,!?:;<\xc2\xa0])~\1\2 \3~gI

# препинания после [сноски] {в конце строки} везде
s~(<a l:href="#n_[0-9]+" type="note">[^<]+</a>)([,!?.:;]+)($fib){0,2}~\2\3\1~gI

# препинания после [сноски] в конце строки
s~(<a l:href="#n_[0-9]+" type="note">[^<]+</a>)([,.!?:;…])($fib){1,2}(</[pv]>)?$~\2\3\1\4~gI

# вставка пробела после [сноски]
s~\s(<a l:href="#n_[0-9]+" type="note">[^<]+</a>)($sib){0,2}([[:alnum:]–—([-])~\1 \2\3~gI

# снятие курсива со сноски
s~(<a l:href="#n_[0-9]+" type="note">)($sib){1,2}([^><]+?)($fib){1,2}(</a>)~\1\2\3\4\5~gI


